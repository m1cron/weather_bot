group = 'ru.micron'
version = '1.0'
rootProject.wrapper.gradleVersion = '6.5'

buildscript {
    ext {
        ver = [
                springBoot              : '2.3.3.RELEASE',
                dependencyManagement    : '1.0.11.RELEASE',
                lombok                  : '1.18.12',
                openFeign               : '2.2.3.RELEASE',
                logback                 : '1.2.3',
                logstashLogbackEncoder  : '6.2',
                h2                      : '1.4.200'
        ]
    }

    repositories {
        mavenCentral()
        mavenLocal()
    }

    dependencies {
        classpath "io.spring.gradle:dependency-management-plugin:$ver.dependencyManagement",
                "org.springframework.boot:spring-boot-gradle-plugin:$ver.springBoot"
    }
}

defaultTasks 'clean', 'build'

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {
        imports {
            mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Hoxton.SR8'
        }
    }

    targetCompatibility = sourceCompatibility = JavaVersion.VERSION_11

    test {
        systemProperty 'spring.profiles.active', 'test'
        useJUnitPlatform()
    }
}

allprojects {
    repositories.addAll(rootProject.buildscript.repositories)
}